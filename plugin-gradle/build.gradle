apply plugin: 'java'
apply plugin: 'maven'

sourceCompatibility = '1.7'

repositories {
    mavenLocal()
    mavenCentral()
}

dependencies {
    compile gradleApi()
    compile project(path: ':dbtools-gen')
}

// ./gradlew clean check installLocal
task installLocal(type: Upload) {
    description "Installs the artifacts to the local Maven repository."
    configuration = configurations['archives']
    repositories {
        mavenDeployer {
            repository(url: "file://${System.env.HOME}/.m2/repository/")
            pom.groupId = GROUP
            pom.artifactId = POM_ARTIFACT_ID
            pom.version = VERSION_NAME
            pom.packaging = POM_PACKAGING
            uniqueVersion = false
            // don't put in a timestamp on the filename (other project will not be able to depend on it)

            // change dependency to internal projects to an proper gav
            pom.whenConfigured {pom ->
                def dep = pom.dependencies.find { dep ->
                    dep.artifactId == 'dbtools-gen'
                }

                dep.version = pom.version
                dep.groupId = pom.groupId
            }
        }
    }
}

// DEPLOY to Maven Central (this must be the last line of this file)
// ./gradlew clean check uploadArchives
apply from: rootProject.file('gradle/gradle-mvn-push.gradle')
